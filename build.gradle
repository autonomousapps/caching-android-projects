buildscript {
    ext.kotlin_version = "1.3.10"
    repositories {
        google()
        jcenter()

        // For build scan nightlies
        maven { url 'https://repo.gradle.org/gradle/enterprise-libs-snapshots-local' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.0-rc01'
        classpath 'com.gradle:build-scan-plugin:latest.integration'//2.1-20181127204740-master
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: "com.gradle.build-scan"
buildScan {
    publishAlways()

    def scanServer = System.getenv("GE_EXPERIMENT")
    if (scanServer != null) {
        // Using GE experimental server
        server = scanServer
        captureTaskInputFiles = true
    } else {
        // We're using the public server
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

subprojects {
    repositories {
        google()
        jcenter()
    }
    pluginManager.withPlugin("kotlin-kapt") {
        kapt.useBuildCache = true
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
